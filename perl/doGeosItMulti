#!/usr/bin/perl -w

#------------------------------------------------------------------------------
#          Harvard University Atmospheric Chemistry Modeling Group            !
#------------------------------------------------------------------------------
#BOP
#
# !MODULE: doGeosItMulti
#
# !DESCRIPTION: This Perl script is the driver script for Geos5Driver, the
#  Fortran code for processing GEOS-IT met field data.  This script
#  submits several executables in separate threads to achieve coarse-grain
#  parallelization.
#\\
#\\
# !USES:
#
  require 5.003;        # need this version of Perl or newer
  use strict;           # Do not allow symbolic references
#
# !PUBLIC MEMBER FUNCTIONS:
#
# readInputConfig($$)
# replaceDateTokens($)
# replaceSystemTokens($)
# checkDir($)
# runJob($)
# main()
#
# !PUBLIC DATA MEMBERS:
#
  #============================================================
  # Define global variables (seen in all subroutines below)
  #============================================================
  our $CODE_DIRECTORY    = "";
  our $DATE_FILE         = "";
  our $DATE_STRING       = "";
  our $FILE_DATE_STRING  = "";
  our $DAY_STRING        = "";
  our $EMAIL_STRING      = "";
  our $EXECUTABLE        = "";
  our $HOME_DIRECTORY    = "";
  our $JOB_DIRECTORY     = "";
  our $JOB_FILE          = "";
  our $LOG_DIRECTORY     = "";
  our $LOG_ERR           = "";
  our $LOG_FILE          = "";
  our $MET_DIRECTORY     = "";
  our $MONTH_STRING      = "";
  our $SUBMIT            = "";
  our $TEMP_DIRECTORY    = "";
  our $USER_ID           = "";
  our $YEAR_STRING       = "";
#
# !CALLING SEQUENCE:
#  doGeosItNulti YYYYMMDD
#
# !REMARKS:
#  Also used for GEOS-IT data.
#
# !REVISION HISTORY: 
#  See git history. This file is based on the equivalent files in GEOS_FP and MERRA2.
#EOP
#------------------------------------------------------------------------------
#          Harvard University Atmospheric Chemistry Modeling Group            !
#------------------------------------------------------------------------------
#BOP
#
# !IROUTINE: readInputConfig
#
# !DESCRIPTION:  Routine readInputConfig reads the default values for 
#  input quantities and also extract date information from passed argument.
#\\
#\\
# !INTERFACE:
#
sub readInputConfig($$) {
#
# !INPUT PARAMETERS:
#
  # $fileName : File containing default settings to be read
  # $date     : YYYYMMDD date for which to process met data
  my ( $fileName, $date ) = @_;
#
# !CALLING SEQUENCE:
# &readInputConfig( FILENAME, DATE );
#
# !REVISION HISTORY:
#  20 Jan 2012 - R. Yantosca - Initial version
#  07 Aug 2013 - R. Yantosca - Now also get value for $MET_DIRECTORY
#EOP
#------------------------------------------------------------------------------
#BOC
#
# !LOCAL VARIABLES:
#
  my $line = "";   
  my @file = "";

  #--------------------------------------
  # Read variable settings from the file
  #--------------------------------------

  # Read defaults file into an array (remove newlines)
  open( I, "<$fileName" ) or die "Cannot open $fileName!\n";
  chomp( @file = <I> );
  close( I );

  # Loop thru each line in the file
  for ( my $i = 0; $i < scalar( @file ); $i++ ) {

    if ( $file[$i] =~ "==> Submit Statement" ) {
      $SUBMIT = $file[++$i];
       
    } elsif ( $file[$i] =~ "==> Code Directory" ) {
      $CODE_DIRECTORY = $file[++$i];

    } elsif ( $file[$i] =~ "==> Job Directory" ) {
      $JOB_DIRECTORY = $file[++$i];

    } elsif ( $file[$i] =~ "==> Log Directory" ) {
      $LOG_DIRECTORY  = $file[++$i];

    } elsif ( $file[$i] =~ "==> Temporary Directory" ) {
      $TEMP_DIRECTORY = $file[++$i];

    } elsif ( $file[$i] =~ "==> Program Executable" ) {
      $EXECUTABLE = $file[++$i];

    } elsif ( $file[$i] =~ "==> Emails for Notification" ) {
      $EMAIL_STRING = $file[++$i];

    } elsif ( $file[$i] =~ "==> Raw Met Data Directory" ) {
      $MET_DIRECTORY = $file[++$i];
    }
  }

  # Extract date information
  $DATE_STRING  = "$date";
  $YEAR_STRING  = substr( $date, 0, 4 );
  $MONTH_STRING = substr( $date, 4, 2 );
  $DAY_STRING   = substr( $date, 6, 2 );

  # Return normally
  return(0);
}
#EOC
#------------------------------------------------------------------------------
#          Harvard University Atmospheric Chemistry Modeling Group            !
#------------------------------------------------------------------------------
#BOP
#
# !IROUTINE: replaceDateTokens
#
# !DESCRIPTION: Subroutine replaceDateTokens replaces date tokens in a variable.
#\\
#\\
# !INTERFACE:
#
sub replaceDateTokens($) {
#
# !INPUT PARAMETERS:
#
  # $var: Name of the string in which to replace tokens
  my ( $var ) = @_; 
#
# !CALLING SEQUENCE:
#  $var = &replaceDateTokens( $var );
#
# !REVISION HISTORY:
#  20 Jan 2012 - R. Yantosca - Initial version
#EOP
#------------------------------------------------------------------------------
#BOC

  # Replace tokens in the variable names
#  $var =~ s/{DSTR}/$DATE_STRING/g;
  $var =~ s/YYYY/$YEAR_STRING/g;
  $var =~ s/MM/$MONTH_STRING/g;
  $var =~ s/DD/$DAY_STRING/g;
 
  # Return to calling program
  return( $var );
}
#EOC
#------------------------------------------------------------------------------
#          Harvard University Atmospheric Chemistry Modeling Group            !
#------------------------------------------------------------------------------
#BOP
#
# !IROUTINE: replaceSystemTokens
#
# !DESCRIPTION: Subroutine replaceSystemTokens replaces system-related tokens
#  in a variable.  
#\\
#\\
# !INTERFACE:
#
sub replaceSystemTokens($) {
#
# !INPUT PARAMETERS:
#
  # $var: Name of the string in which to replace tokens
  my ( $var ) = @_; 
#
# !CALLING SEQUENCE:
#  $var = &replaceSystemTokens( $var );
#
# !REVISION HISTORY:
#  20 Jan 2012 - R. Yantosca - Initial version
#EOP
#------------------------------------------------------------------------------
#BOC

  # Replace tokens in the variable names
  $var =~ s/{HOME}/$HOME_DIRECTORY/g;
  $var =~ s/{JOB}/$JOB_FILE/g;
  $var =~ s/{LOGERR}/$LOG_ERR/g;
  $var =~ s/{USER}/$USER_ID/g;
 
  # Return to calling program
  return( $var );
}
#EOC
#------------------------------------------------------------------------------
#          Harvard University Atmospheric Chemistry Modeling Group            !
#------------------------------------------------------------------------------
#BOP
#
# !IROUTINE: checkDir
#
# !DESCRIPTION: Subroutine checkDir checks to see if a directory exists.
#\\
#\\
# !INTERFACE:
#
sub checkDir($) {
#
# !INPUT PARAMETERS:
#
  # $dir : Directory to be checked
  my ( $dir ) = @_;
#
# !CALLING SEQUENCE:
#  &checkDir( $dir );
#
# !REVISION HISTORY:
#  20 Jan 2012 - R. Yantosca - Initial version
#EOP
#------------------------------------------------------------------------------
#BOC

  # Stop with error if root directory doesn't exist (excludes year and month)
  if ( !( -d $dir ) ) { 
    print "doGeosItMulti: ERROR: Directory does not exist: $dir\n";
    exit(1)
  }
  
  # Otherwise return normally
  return(0);
}
#EOC
#------------------------------------------------------------------------------
#          Harvard University Atmospheric Chemistry Modeling Group            !
#------------------------------------------------------------------------------
#BOP
#
# !IROUTINE: makeJobFile
#
# !DESCRIPTION: Subroutine makeJobFile creates a job script for each 
#  process.  The thread number (0,1,2) is appended to the script names and
#  log files.
#\\
#\\
# !INTERFACE:
#
sub makeJobFile($) {
#
#
# !INPUT PARAMETERS:
#
  my ( $thread ) = @_;   # Thread number 
#
# !CALLING SEQUENCE:
#  &runJob( 0 );   # Runs executable GeosItDriver0.x
#  &runJob( 1 );   # Runs executable GeosItDriver1.x, etc.
#
# !REVISION HISTORY:
#  20 Jan 2012 - R. Yantosca - Initial version
#EOP
#------------------------------------------------------------------------------
#BOC
#
# !LOCAL VARIABLES:
#
  # Pick the proper file types
  my $fileTypes = "";    
  if    ( $thread == 0 ) { $fileTypes = "A1 I3";                      }
  elsif ( $thread == 1 ) { $fileTypes = "A3cld A3dyn";                }
  elsif ( $thread == 2 ) { $fileTypes = "A3mstC A3mstE";              }
  else                   { print "Invalid thread number!\n"; exit(1); }

  # Create shadow variables
  my $body      = "File types $fileTypes are now created for $DATE_STRING. ";
     $body     .= "Files shall be visible on /mnt/gcgrid within 15 minutes.";
  my $email     = $EMAIL_STRING;
  my $exeFile   = $EXECUTABLE;
  my $jobFile   = $JOB_FILE;
  my $logFile   = $LOG_FILE;
  my $subject   = "GEOS-IT $fileTypes met fields for $DATE_STRING are ready!";
  my $mailCmd   = qq( echo "$body" > X | mail -s "$subject" $email < X; rm -f X );

  #-----------------------------------------
  # Create a file with the date of the data
  #-----------------------------------------

  # Save date in a file to pass to the F90 code
  open( O, ">$DATE_FILE" ) or die "Cannot open $DATE_FILE!\n";
  #ewl  print O "$DATE_STRING\n";
  print O "$FILE_DATE_STRING\n";
  close( O );

  #-----------------------------------------
  # Define job script in a "here" document
  #-----------------------------------------

  # Replace tokens for thread number in local shadow variables
  $exeFile =~ s/{THREAD}/$thread/g;
  $jobFile =~ s/{THREAD}/$thread/g;
  $logFile =~ s/{THREAD}/$thread/g;

  # Create HERE document
  my $txt =<<EOF;
#!/usr/bin/perl -w

# Local variables
my \$sLog  = qq($logFile);

# Make files world-readable
umask(022);

# Echo starting time
qx( echo "===========================================" >> \$sLog );
qx( echo "doGeosItMulti: Extracting GEOS-IT Met Data!" >> \$sLog );
qx( echo "Started on `date`"                           >> \$sLog );
qx( echo                                               >> \$sLog );

# List the temp directory first to force a directory mount
qx( ls $TEMP_DIRECTORY > /dev/null );

# Run the GEOS-5 data extraction code
qx( umask 022; cd $CODE_DIRECTORY; ./$exeFile < $DATE_FILE >> \$sLog );

# Move files from the temp directory to the data directory
qx( ./moveGeosIt $DATE_STRING $fileTypes >> \$sLog );

# Send an email that the job is finished
# if ( \$\? == 0 ) { qx( $mailCmd ); }

# Remove the date file
qx( rm -f $DATE_FILE >> /dev/null ); 

# Echo ending time
qx( echo                      >> \$sLog );
qx( echo "Finished on `date`" >> \$sLog );
qx( echo                      >> \$sLog );

exit(0);
EOF

  # Write job script to a file
  open( O, ">$jobFile" ) or die "Cannot open $jobFile\n";
  print O "$txt\n";
  close( O );

  # Make run script executable
  chmod( 0755, $jobFile );

  # Return normally
  return( 0 );
}
#EOC
#------------------------------------------------------------------------------
#          Harvard University Atmospheric Chemistry Modeling Group            !
#------------------------------------------------------------------------------
#BOP
#
# !IROUTINE: runJobs
#
# !DESCRIPTION: Subroutine runJob executes 3 job scripts simultaneiously,
#  each in a different Unix process.
#\\
#\\
# !INTERFACE:
#
sub runJobs() {
#
# !CALLING SEQUENCE:
#  &runJobs();     # Submits all job scripts simultaneously
#
# !REVISION HISTORY:
#  20 Jan 2012 - R. Yantosca - Initial version
#EOP
#------------------------------------------------------------------------------
#BOC
#
# !LOCAL VARIABLES:
#
  my @threads  = ( 0 .. 2 );
  my @job      = qw( a b c );

  foreach ( my $i=0; $i<=2; $i++ ) {
    $job[$i] = $JOB_FILE;
    $job[$i] =~ s/{THREAD}/$threads[$i]/g; 
  }

  # Command to run all jobs in the background
  # NOTE: We need to use a TCSH script to get jobs to submit in parallel
  my $cmd = "./runMet $job[0] $job[1] $job[2]";

  # Submit all jobs in multiple threads
  qx( $cmd );

  # Return normally
  return( 0 );
}
#EOC
#------------------------------------------------------------------------------
#          Harvard University Atmospheric Chemistry Modeling Group            !
#------------------------------------------------------------------------------
#BOP
#
# !IROUTINE: main
#
# !DESCRIPTION: Routine main is the driver routine for the doGeos5 script.
#\\
#\\
# !INTERFACE:
#
sub main() {
#
# !CALLING SEQUENCE:
#  &main();
#
# !REVISION HISTORY:
#  24 Oct 2011 - R. Yantosca - Initial version, based on GEOS_5/perl/doGeos5
#EOP
#------------------------------------------------------------------------------
#
# !LOCAL VARIABLES:
#
  # Scalars
  my $status  = 0;
  my $listCmd = "";
  my $cmd     = "";
  my $file    = "";

  # Arrays
  my @files   = ();

  #-------------------------------------------
  # Error check arguments
  #-------------------------------------------
  if ( scalar( @ARGV ) == 0 ) { 
    print "Usage: doGeosItMulti YYYYMMDD\n"; 
    exit(1);
  }

  #-------------------------------------------
  # Read input configuration file and date info
  #-------------------------------------------
  &readInputConfig( "doGeosIt.input", $ARGV[0] );

  #---------------------------------------
  # Replace date-related tokens, if any
  #---------------------------------------
  $MET_DIRECTORY     = &replaceDateTokens( $MET_DIRECTORY  );
  $TEMP_DIRECTORY    = &replaceDateTokens( $TEMP_DIRECTORY );

  #-------------------------------------------
  # Define other variables
  #-------------------------------------------
  # Source file date string to include dashes
  $FILE_DATE_STRING = "$YEAR_STRING-$MONTH_STRING-$DAY_STRING";

  # File to pass date to F90 code
  $DATE_FILE    = "$TEMP_DIRECTORY/$FILE_DATE_STRING.$$";

  # Log error file
  $LOG_ERR      = "$LOG_DIRECTORY/log.doGeosItMulti.error-{THREAD}";

  # Log file
  $LOG_FILE     = "$LOG_DIRECTORY/log.doGeosItMulti.$DATE_STRING.$$-{THREAD}";

  # Job script
  $JOB_FILE     = "$JOB_DIRECTORY/job.doGeosItMulti.$DATE_STRING.$$-{THREAD}";  

  #-------------------------------------------
  # Check if files have been downloaded properly
  #-------------------------------------------
  # Test if at least some files are present in the met dir with expected format
  $listCmd = "ls -1 $MET_DIRECTORY/GEOS.it.asm.*"."$FILE_DATE_STRING"."*";
  chomp( @files = qx( $listCmd ) );
  foreach $file ( @files ) { if ( -f  $file ) { $status++; } }

  #-------------------------------------------
  # Get system variables
  #-------------------------------------------
  # User ID (remove newline)
  $USER_ID = qx( echo \$USER );
  chomp( $USER_ID );
  
  # Home directory (remove newline)
  $HOME_DIRECTORY = qx( echo \$HOME );
  chomp( $HOME_DIRECTORY );

  #-------------------------------------------
  # Replace system tokens, if any
  #-------------------------------------------
  $JOB_DIRECTORY     = &replaceSystemTokens( $JOB_DIRECTORY  );
  $LOG_DIRECTORY     = &replaceSystemTokens( $LOG_DIRECTORY  );
  $LOG_ERR           = &replaceSystemTokens( $LOG_ERR        );
  $LOG_FILE          = &replaceSystemTokens( $LOG_FILE       );
  $JOB_FILE          = &replaceSystemTokens( $JOB_FILE       );
  $SUBMIT            = &replaceSystemTokens( $SUBMIT         );

  #-------------------------------------------
  # Check if directories exists
  #-------------------------------------------
  &checkDir( $CODE_DIRECTORY );
  &checkDir( $JOB_DIRECTORY  );
  &checkDir( $LOG_DIRECTORY  );
  &checkDir( $TEMP_DIRECTORY );
  &checkDir( $MET_DIRECTORY  );

  #-------------------------------------------
  # Write the job file and submit it to the queue
  #-------------------------------------------
  &makeJobFile( 0 );   # Creates A1, I3, (and CN if necessary) files
  &makeJobFile( 1 );   # Creates A3cld and A3dyn files
  &makeJobFile( 2 );   # Creates A3mstC and A3mstE files

  #-------------------------------------------
  # Run jobs
  #-------------------------------------------
  &runJobs();

  #-------------------------------------------
  # Done!
  #-------------------------------------------
  return(0);
}
#EOC

#------------------------------------------------------------------------------

# Start main program
main();

# Exit normally
exit(0);
